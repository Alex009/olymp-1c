Процедура КнопкаСформироватьНажатие(Кнопка)
	ТабДок = ЭлементыФормы.ПолеТабличногоДокумента;
	
	Долги = Новый ТаблицаЗначений;
	Долги.Колонки.Добавить("Кто");
	Долги.Колонки.Добавить("Кому");
	Долги.Колонки.Добавить("Сумма");
	
	Сальдо = Новый ТаблицаЗначений;
	Сальдо.Колонки.Добавить("Кто");
	Сальдо.Колонки.Добавить("Сумма");
	
	Взаиморасчеты = Новый ТаблицаЗначений;
	Взаиморасчеты.Колонки.Добавить("Кто");
	Взаиморасчеты.Колонки.Добавить("Кому");
	Взаиморасчеты.Колонки.Добавить("Сумма");
	
	// получаем долги
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЖурналОперацийДвиженияССубконто.Организации,
	               |	ЖурналОперацийДвиженияССубконто.СубконтоКт1,
	               |	ЖурналОперацийДвиженияССубконто.Сумма
	               |ИЗ
	               |	РегистрБухгалтерии.ЖурналОпераций.ДвиженияССубконто КАК ЖурналОперацийДвиженияССубконто
	               |ГДЕ
	               |	ЖурналОперацийДвиженияССубконто.СчетКт.Код = ""76""";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДолгиЗапись = Долги.Добавить();                		
		ВзаиморасчетыЗапись = Взаиморасчеты.Добавить();
		
		ДолгиЗапись.Кто = Выборка.Организации;
		ДолгиЗапись.Кому = Выборка.СубконтоКт1;
		ДолгиЗапись.Сумма = Выборка.Сумма; 
		
		ВзаиморасчетыЗапись.Кто = ДолгиЗапись.Кто;		
		ВзаиморасчетыЗапись.Кому = ДолгиЗапись.Кому;
		ВзаиморасчетыЗапись.Сумма = ДолгиЗапись.Сумма;
	КонецЦикла;
	
	// получаем сальдо
	Для Каждого Долг Из Долги Цикл
		СальдоЗаписьКто = Сальдо.Найти(Долг.Кто,"Кто");		
		СальдоЗаписьКому = Сальдо.Найти(Долг.Кому,"Кто");
		
		Если СальдоЗаписьКто = Неопределено Тогда
			СальдоЗаписьКто = Сальдо.Добавить();
			СальдоЗаписьКто.Кто = Долг.Кто;
			СальдоЗаписьКто.Сумма = 0;
		КонецЕсли;
		
		Если СальдоЗаписьКому = Неопределено Тогда
			СальдоЗаписьКому = Сальдо.Добавить();
			СальдоЗаписьКому.Кто = Долг.Кому;
			СальдоЗаписьКому.Сумма = 0;
		КонецЕсли;
		
		СальдоЗаписьКто.Сумма = СальдоЗаписьКто.Сумма - Долг.Сумма;		
		СальдоЗаписьКому.Сумма = СальдоЗаписьКому.Сумма + Долг.Сумма;
	КонецЦикла;
	
	// расчитываем взаиморасчеты
	// ищем циклы 
	ЦиклРезультат = НайтиЦикл(Взаиморасчеты,Неопределено);
	Пока ЦиклРезультат <> Неопределено Цикл
		Минимальный = ЦиклРезультат[0];
		Для Каждого Звено Из ЦиклРезультат Цикл
			Если Звено.Сумма < Минимальный.Сумма Тогда
				Минимальный = Звено;
			КонецЕсли;
		КонецЦикла;
		// уничтожаем цикл
		Для Каждого Звено Из ЦиклРезультат Цикл
			Для Каждого ВзаиморасчетыЗапись Из Взаиморасчеты Цикл
				Если ВзаиморасчетыЗапись.Кто <> Звено.Кто Тогда
					Продолжить;
				КонецЕсли;
				
				Если ВзаиморасчетыЗапись.Кому <> Звено.Кому Тогда
					Продолжить;
				КонецЕсли;
				
				ВзаиморасчетыЗапись.Сумма = ВзаиморасчетыЗапись.Сумма - Минимальный.Сумма;
			
				Если ВзаиморасчетыЗапись.Сумма = 0 Тогда
					Взаиморасчеты.Удалить(ВзаиморасчетыЗапись);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла; 	
		ЦиклРезультат = НайтиЦикл(Взаиморасчеты,Неопределено);
	КонецЦикла;	
	// ищем цепи
	ЦепьРезультат = НайтиЦепь(Взаиморасчеты,Неопределено);
	Пока ЦепьРезультат <> Неопределено Цикл
		Запись = Новый Массив(2); 
		Для Каждого ВзаиморасчетыЗапись Из Взаиморасчеты Цикл
			Для i = 0 по 1 Цикл
				Если ВзаиморасчетыЗапись.Кто = ЦепьРезультат[i].Кто И ВзаиморасчетыЗапись.Кому = ЦепьРезультат[i].Кому Тогда
					Запись[i] = ВзаиморасчетыЗапись;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Если Запись[0].Сумма > Запись[1].Сумма Тогда
			Второму = Запись[0].Сумма - Запись[1].Сумма;

			Запись[0].Сумма = Второму;
			Запись[1].Кто = Запись[0].Кто;
		ИначеЕсли Запись[0].Сумма <= Запись[1].Сумма Тогда
			Третьему = Запись[1].Сумма - Запись[0].Сумма;

			Запись[0].Кому = Запись[1].Кому;
			Запись[1].Сумма = Третьему;
		КонецЕсли;
		
		ЦепьРезультат = НайтиЦепь(Взаиморасчеты,Неопределено);
	КонецЦикла;
	
	Макет = ПолучитьМакет("Макет");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаВзаиморасчеты = Макет.ПолучитьОбласть("ШапкаВзаиморасчеты");
	ОбластьСтрокаВзаиморасчеты = Макет.ПолучитьОбласть("СтрокаВзаиморасчеты");
	ОбластьШапкаСальдо = Макет.ПолучитьОбласть("ШапкаСальдо");
	ОбластьСтрокаСальдо = Макет.ПолучитьОбласть("СтрокаСальдо");
	
	ТабДок.Очистить();
	
	ОбластьЗаголовок.Параметры[0] = "Долги";
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаВзаиморасчеты);
	Для Каждого Запись Из Долги Цикл
		ОбластьСтрокаВзаиморасчеты.Параметры[0] = Запись.Кто;
		ОбластьСтрокаВзаиморасчеты.Параметры[1] = Запись.Кому;
		ОбластьСтрокаВзаиморасчеты.Параметры[2] = Запись.Сумма;
		ТабДок.Вывести(ОбластьСтрокаВзаиморасчеты);
	КонецЦикла;
	
	ОбластьЗаголовок.Параметры[0] = "Сальдо";
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаСальдо);
	Для Каждого Запись Из Сальдо Цикл
		ОбластьСтрокаСальдо.Параметры[0] = Запись.Кто;
		ОбластьСтрокаСальдо.Параметры[1] = Запись.Сумма;
		ТабДок.Вывести(ОбластьСтрокаСальдо);
	КонецЦикла;
	
	ОбластьЗаголовок.Параметры[0] = "Взаиморасчеты";
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапкаВзаиморасчеты);
	Для Каждого Запись Из Взаиморасчеты Цикл
		ОбластьСтрокаВзаиморасчеты.Параметры[0] = Запись.Кто;
		ОбластьСтрокаВзаиморасчеты.Параметры[1] = Запись.Кому;
		ОбластьСтрокаВзаиморасчеты.Параметры[2] = Запись.Сумма;
		ТабДок.Вывести(ОбластьСтрокаВзаиморасчеты);
	КонецЦикла;
	
КонецПроцедуры


Функция НайтиЦикл(Взаиморасчеты,Результат)
	РезультатВременный = Неопределено;
	Если Результат = Неопределено Тогда
		РезультатВременный = Новый ТаблицаЗначений;
		РезультатВременный.Колонки.Добавить("Кто");	
		РезультатВременный.Колонки.Добавить("Кому");
		РезультатВременный.Колонки.Добавить("Сумма");
	Иначе
		РезультатВременный = Результат.Скопировать();
	КонецЕсли;
	
	Для Каждого ВзаиморасчетыЗапись Из Взаиморасчеты Цикл
		Количество = РезультатВременный.Количество();
		Если Количество > 0 Тогда
			Если РезультатВременный[Количество - 1].Кому <> ВзаиморасчетыЗапись.Кто Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		РезультатЗапись = РезультатВременный.Добавить();
		РезультатЗапись.Кто = ВзаиморасчетыЗапись.Кто;		
		РезультатЗапись.Кому = ВзаиморасчетыЗапись.Кому;
		РезультатЗапись.Сумма = ВзаиморасчетыЗапись.Сумма;
		
		Если РезультатВременный.Количество() > 1 Тогда
			Если РезультатВременный[0].Кто = РезультатЗапись.Кому Тогда
				// Цикл найден 
				Возврат РезультатВременный; 
			КонецЕсли;
		КонецЕсли;
		
		ЦиклР = НайтиЦикл(Взаиморасчеты,РезультатВременный);
		Если ЦиклР <> Неопределено Тогда
			Возврат ЦиклР;
		Иначе
			РезультатВременный.Удалить(РезультатЗапись);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

Функция НайтиЦепь(Взаиморасчеты,Результат)
	РезультатВременный = Неопределено;
	Если Результат = Неопределено Тогда
		РезультатВременный = Новый ТаблицаЗначений;
		РезультатВременный.Колонки.Добавить("Кто");	
		РезультатВременный.Колонки.Добавить("Кому");
		РезультатВременный.Колонки.Добавить("Сумма");
	Иначе
		РезультатВременный = Результат.Скопировать();
	КонецЕсли;
	
	Для Каждого ВзаиморасчетыЗапись Из Взаиморасчеты Цикл
		Количество = РезультатВременный.Количество();
		Если Количество > 0 Тогда
			Если РезультатВременный[Количество - 1].Кому <> ВзаиморасчетыЗапись.Кто Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		РезультатЗапись = РезультатВременный.Добавить();
		РезультатЗапись.Кто = ВзаиморасчетыЗапись.Кто;		
		РезультатЗапись.Кому = ВзаиморасчетыЗапись.Кому;
		РезультатЗапись.Сумма = ВзаиморасчетыЗапись.Сумма;
		
		Если РезультатВременный.Количество() > 1 Тогда
			// цепь из больше чем двух элементов найдена
			Возврат РезультатВременный;
		КонецЕсли;
		
		Цепь = НайтиЦепь(Взаиморасчеты,РезультатВременный);
		Если Цепь <> Неопределено Тогда     
			Возврат Цепь;
		Иначе
			РезультатВременный.Удалить(РезультатЗапись);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

