Процедура КнопкаСформироватьНажатие(Кнопка)
	ТабДок = ЭлементыФормы.ПолеТабличногоДокумента1;
	
	Макет = ПолучитьМакет("Макет");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьДанные = Макет.ПолучитьОбласть("Данные");
	
	Взаимосвязи = Новый Соответствие;
	Обход = Новый Массив;
	
	// организации должники
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЖурналОперацийДвиженияССубконто.Организации КАК Организация1,
	               |	ЖурналОперацийДвиженияССубконто.СубконтоДт1 КАК Организация2
	               |ИЗ
	               |	РегистрБухгалтерии.ЖурналОпераций.ДвиженияССубконто КАК ЖурналОперацийДвиженияССубконто
	               |ГДЕ
	               |	ЖурналОперацийДвиженияССубконто.СчетДт.Код = ""79""
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЖурналОперацийДвиженияССубконто.Организации";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
		
	Пока Выборка.Следующий() Цикл
		ДобавитьВзаимосвязь(Взаимосвязи,Выборка);
	КонецЦикла;
	
	// организации кредиторы
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЖурналОперацийДвиженияССубконто.Организации КАК Организация1,
	               |	ЖурналОперацийДвиженияССубконто.СубконтоКт1 КАК Организация2
	               |ИЗ
	               |	РегистрБухгалтерии.ЖурналОпераций.ДвиженияССубконто КАК ЖурналОперацийДвиженияССубконто
	               |ГДЕ
	               |	ЖурналОперацийДвиженияССубконто.СчетДт.Код = ""51""
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ЖурналОперацийДвиженияССубконто.Организации";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
		
	Пока Выборка.Следующий() Цикл
		ДобавитьВзаимосвязь(Взаимосвязи,Выборка);
	КонецЦикла;
	
	
	Пока Взаимосвязи.Количество() > 0 Цикл
		Максимальный = Неопределено;
		Для Каждого Запись Из Взаимосвязи Цикл
			Если Максимальный = Неопределено Тогда
				Максимальный = Запись;
			ИначеЕсли Максимальный.Значение.Количество() < Запись.Значение.Количество() Тогда
				Максимальный = Запись;
			КонецЕсли;
		КонецЦикла;
		
		Обход.Добавить(Максимальный.Ключ);
		
		Для Каждого Связь Из Максимальный.Значение Цикл 
			Взаимосвязь = Взаимосвязи.Получить(Связь);
			
			Индекс = Взаимосвязь.Найти(Максимальный.Ключ);
			Взаимосвязь.Удалить(Индекс);
			Если Взаимосвязь.Количество() = 0 Тогда
				Взаимосвязи.Удалить(Связь);
			КонецЕсли;
		КонецЦикла;
		
		Взаимосвязи.Удалить(Максимальный.Ключ);
	КонецЦикла;

	ТабДок.Очистить();
	
	ТабДок.Вывести(ОбластьЗаголовок);

	Номер = 1;
	Для Каждого Организация Из Обход Цикл
		ОбластьДанные.Параметры[0] = Номер;
		ОбластьДанные.Параметры[1] = Организация.Код;
		ОбластьДанные.Параметры[2] = Организация.Наименование;
		
		ТабДок.Вывести(ОбластьДанные);
		
		Номер = Номер + 1;
	КонецЦикла;	
	
КонецПроцедуры

Процедура ДобавитьВзаимосвязь(Взаимосвязи,Выборка)
	Взаимосвязи1 = Взаимосвязи.Получить(Выборка.Организация1);
	Взаимосвязи2 = Взаимосвязи.Получить(Выборка.Организация2);
	
	Если Взаимосвязи1 = Неопределено Тогда
		Взаимосвязи1 = Новый Массив;
		Взаимосвязи.Вставить(Выборка.Организация1,Взаимосвязи1);
	КонецЕсли;
	
	Если Взаимосвязи2 = Неопределено Тогда
		Взаимосвязи2 = Новый Массив;
		Взаимосвязи.Вставить(Выборка.Организация2,Взаимосвязи2);
	КонецЕсли;
	
	Если Взаимосвязи1.Найти(Выборка.Организация2) = Неопределено Тогда
		Взаимосвязи1.Добавить(Выборка.Организация2);
	КонецЕсли;
	
	Если Взаимосвязи2.Найти(Выборка.Организация1) = Неопределено Тогда
		Взаимосвязи2.Добавить(Выборка.Организация1);
	КонецЕсли;	
КонецПроцедуры
