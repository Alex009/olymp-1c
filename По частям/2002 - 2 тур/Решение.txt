&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	ЭтотОбъект=РеквизитФормыВЗначение("Отчет");
	Макет = ЭтотОбъект.ПолучитьМакет("Макет"); 
	
 	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЖурналПроводокОстатки.Субконто1 КАК Рулон,
		|	ЖурналПроводокОстатки.Субконто2 КАК Длина,
		|	ЖурналПроводокОстатки.Субконто3 КАК Цена,
		|	ЖурналПроводокОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводок.Остатки КАК ЖурналПроводокОстатки
		|ГДЕ
		|	ЖурналПроводокОстатки.Счет = &Счет
		|	И ЖурналПроводокОстатки.Субконто1.Ссылка = &Рулон
		|
		|УПОРЯДОЧИТЬ ПО
		|	Длина";

	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ПланСчетов.Товары); 	
	Запрос.УстановитьПараметр("Рулон", Рулон);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Остатки = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Данные = Новый Структура;
		Данные.Вставить("Длина", Выборка.Длина);
		Данные.Вставить("Цена", Выборка.Цена);
		Данные.Вставить("Количество", Выборка.Количество);
		
		Остатки.Добавить(Данные);
	КонецЦикла;
	
	Результат = НайтиЛучшийПуть(Остатки, Неопределено, Длина);
	
	ТабДок.Очистить();
	
	Рулоны = Новый Массив;
	Для Каждого Элемент Из Результат.Путь Цикл
		Найден = Ложь;
		Для Каждого РулонЭлемент Из Рулоны Цикл
			Если РулонЭлемент.Длина = Элемент.Длина Тогда
				РулонЭлемент.Количество = РулонЭлемент.Количество + 1;
				РулонЭлемент.Сумма = РулонЭлемент.Сумма + Элемент.Цена;
				Найден = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Найден = Ложь Тогда
			РулонЭлемент = Новый Структура;
			РулонЭлемент.Вставить("Длина", Элемент.Длина);
			РулонЭлемент.Вставить("Количество", 1);
			РулонЭлемент.Вставить("Сумма", Элемент.Цена);
			
			Рулоны.Добавить(РулонЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	ЗаголовокОбласть = Макет.ПолучитьОбласть("Заголовок");
	ДанныеОбласть = Макет.ПолучитьОбласть("Данные");
	ИтогОбласть = Макет.ПолучитьОбласть("Итог");
	
	ТабДок.Вывести(ЗаголовокОбласть);
	
	Метров = 0;
	Сумма = 0;
	Для Каждого РулонЭлемент Из Рулоны Цикл
		ДанныеОбласть.Параметры.Рулон = Рулон;
		ДанныеОбласть.Параметры.Метраж = РулонЭлемент.Длина;
		ДанныеОбласть.Параметры.Количество = РулонЭлемент.Количество;
		ДанныеОбласть.Параметры.Метров = РулонЭлемент.Длина * РулонЭлемент.Количество;
		ДанныеОбласть.Параметры.Сумма = РулонЭлемент.Сумма;
		
		ТабДок.Вывести(ДанныеОбласть);
		
		Метров = Метров + ДанныеОбласть.Параметры.Метров;
		Сумма = Сумма + ДанныеОбласть.Параметры.Сумма;
	КонецЦикла;
	
	ИтогОбласть.Параметры.Метров = Метров;
	ИтогОбласть.Параметры.Сумма = Сумма;
	ТабДок.Вывести(ИтогОбласть);
КонецПроцедуры

&НаСервере
Функция НайтиЛучшийПуть(Остатки, ПрошлыйРезультат, Цель)
	Если ПрошлыйРезультат = Неопределено Тогда
		ПрошлыйРезультат = Новый Структура;
		ПрошлыйРезультат.Вставить("Длина", 0);
		ПрошлыйРезультат.Вставить("Путь", Новый Массив);
	КонецЕсли;
	
	ЛучшийПуть = Неопределено;
	ЛучшийЭлемент = Неопределено;
	ЛучшаяДлина = 0;
	
	Для Каждого Остаток Из Остатки Цикл
		Если Остаток.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяДлина = ПрошлыйРезультат.Длина + Остаток.Длина;
		
		Если НоваяДлина < Цель Тогда
			Остаток.Количество = Остаток.Количество - 1;
			ПрошлыйРезультат.Длина = НоваяДлина;
			ПрошлыйРезультат.Путь.Добавить(Остаток);
			
			НовыйРезультат = НайтиЛучшийПуть(Остатки, ПрошлыйРезультат, Цель);
			
			ПрошлыйРезультат.Путь.Удалить(ПрошлыйРезультат.Путь.ВГраница());
			ПрошлыйРезультат.Длина = ПрошлыйРезультат.Длина - Остаток.Длина;
			Остаток.Количество = Остаток.Количество + 1;
			
			Если НовыйРезультат = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если НовыйРезультат.Длина >= Цель Тогда
				Если ЛучшаяДлина = 0 ИЛИ (ЛучшаяДлина - Цель) > (НовыйРезультат.Длина - Цель) Тогда
					ЛучшаяДлина = НовыйРезультат.Длина;
					ЛучшийПуть = НовыйРезультат.Путь;
					ЛучшийЭлемент = Неопределено;
				КонецЕсли;  
			КонецЕсли;
		Иначе
			Если ЛучшаяДлина = 0 ИЛИ (ЛучшаяДлина - Цель) > (НоваяДлина - Цель) Тогда
				ЛучшаяДлина = НоваяДлина;
				ЛучшийЭлемент = Остаток;
				ЛучшийПуть = Неопределено;
			КонецЕсли;  
		КонецЕсли;
		
		Если ЛучшаяДлина = Цель Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЛучшийЭлемент <> Неопределено Тогда
		ЛучшийПуть = Новый Массив;
		Для Каждого Прошлый Из ПрошлыйРезультат.Путь Цикл
			ЛучшийПуть.Добавить(Прошлый);
		КонецЦикла;
		ЛучшийПуть.Добавить(ЛучшийЭлемент);
	КонецЕсли;
	
	Если ЛучшийПуть = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Длина", ЛучшаяДлина);	
	Результат.Вставить("Путь", ЛучшийПуть);
	
	Возврат Результат;
КонецФункции
